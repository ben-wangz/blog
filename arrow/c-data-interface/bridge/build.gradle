plugins {
    id "java-library"
    id "org.bytedeco.gradle-javacpp-build"  version "1.5.10"
}

def javacppVersion = "1.5.10"
def arrowVersion = "15.0.2"

dependencies {
    api "org.bytedeco:javacpp:$javacppVersion"
    implementation "org.apache.arrow:arrow-c-data:${arrowVersion}"
    implementation "org.apache.arrow:arrow-vector:${arrowVersion}"
    implementation "org.apache.arrow:arrow-memory-core:${arrowVersion}"
    implementation "org.apache.arrow:arrow-memory-netty:${arrowVersion}"
    implementation "org.apache.arrow:arrow-format:${arrowVersion}"

    testRuntimeOnly "org.bytedeco:javacpp:$javacppVersion:$javacppPlatform"
    testImplementation "org.junit.jupiter:junit-jupiter:5.9.3"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

tasks.withType(org.bytedeco.gradle.javacpp.BuildTask) {
    includePath = ["$buildDir/$javacppPlatform/include"]
    linkPath = ["$buildDir/$javacppPlatform/lib"]
}

javacppBuildCommand {
    buildCommand = ["bash", project.file("build.sh").absolutePath]
    dependsOn javacppPomProperties
}

javacppBuildParser {
    classOrPackageNames = ["tech.geekcity.arrow.c.data.bridge.*"]
    outputDirectory = file("$buildDir/generated/sources/javacpp/")
}

javacppBuildCompiler {
    copyLibs = true
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

jar {
     manifest {
        attributes "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(" "),
                   "Implementation-Title": "JavaCPP Presets for custom cpp",
                   "Implementation-Vendor": "ben.wangz",
                   "Implementation-Version": project.version,
                   "Specification-Title": "JavaCPP Presets for custom cpp",
                   "Specification-Vendor": "ben.wangz",
                   "Specification-Version": project.version
    }
}

task customJar(type: Jar) {
    from sourceSets.main.output // 获取主代码输出
    dependsOn jar
}

// 将 customJar 任务的输出添加到项目的构建工件中
artifacts {
    archives customJar
}
